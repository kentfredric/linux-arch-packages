# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: aurperl@juster.us

pkgname=perl-log-dispatch
_cpanname="Log-Dispatch"
pkgver=2.29
pkgrel=4
pkgdesc="Dispatches messages to one or more outputs"
arch=('any')
license=(
	'PerlArtistic'
	'GPL'
)
options=('!emptydirs')
depends=(
	'perl>=5.5.0'
	'perl-params-validate>=0.15'
	'perl-module-implementation'
	'perl-try-tiny'
)
url="http://search.cpan.org/~drolsky/$_cpanname"
source=("http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/$_cpanname-$pkgver.tar.gz")
md5sums=('781a4b5a9db2b617d2487dadecf65081')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/$_cpanname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
