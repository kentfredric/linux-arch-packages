# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname=perl-namespace-autoclean
_cpanname="namespace-autoclean"
pkgver=0.13
pkgrel=2
pkgdesc="Keep imports out of your namespace"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.5.0' 'perl-b-hooks-endofscope' 'perl-namespace-clean>=0.20')
makedepends=('perl-moose' 'perl-sub-name')
url="http://search.cpan.org/dist/$_cpanname"
source=("http://search.cpan.org/CPAN/authors/id/B/BO/BOBTFISH/$_cpanname-$pkgver.tar.gz")
md5sums=('ec5527b420ec45d3f930c6345ec23926')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/$_cpanname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
