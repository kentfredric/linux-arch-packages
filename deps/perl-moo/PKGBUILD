# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-moo'
pkgver='1.002000'
pkgrel='1'
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.1' 'perl-devel-globaldestruction>=0.11' 'perl-module-runtime>=0.012' 'perl-role-tiny>=1.002004' 'perl-strictures>=1.004003')
makedepends=('perl-test-fatal>=-0.003')
url='https://metacpan.org/release/Moo'
source=("http://cpan.metacpan.org/authors/id/M/MS/MSTROUT/Moo-${pkgver}.tar.gz")
md5sums=('db8d2e02417afaaead44cff6f6392b7b')
sha512sums=('3320624ed34c1b0cd6dda0ae6d06e06c970a7f388287236d1ba468fe148e2cb5f260abf0d4c06cb422b022f5e4cf7c32626892179ab87b9fb71c11367cce2652')

prepare_env() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Moo-${pkgver}"
}

build() {
  prepare_env
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_env
  make test
}

package() {
  prepare_env
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
