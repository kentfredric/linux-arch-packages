# Maintainer : Kent Fredric <kentnl@cpan.org>

pkgname=perl-file-temp
pkgver=0.2301
pkgrel=1
pkgdesc="return name and handle of a temporary file safely"
arch=('any')
url="https://metacpan.org/release/File-Temp/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://cpan.metacpan.org/authors/id/D/DA/DAGOLDEN/File-Temp-${pkgver}.tar.gz)
md5sums=('75a8aec1916ebbe88988ce68e84ccdd7')
sha1sums=('212d6db8860f5e0b88d122faba363c7086e2d3af')
sha512sums=('7710258c2d8ebdb10671e59bf794a752fcbb6d7e42fe525922773ef33952dadd47b9234b83c8f66cebb3c35298251677a396ff541ee0421f06f884d6ab9d7954')
_pkg_name="File-Temp"
_pkg_root="${_pkg_name}-${pkgver}"

build() {
  cd "${srcdir}/${_pkg_root}"
 
 # Setting these env variables overwrites any command-line-options we don't want...
 export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
   PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
   PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
   PERL5LIB="" PERL_LOCAL_LIB_ROOT="" \
   MODULEBUILDRC=/dev/null

  {
	/usr/bin/perl Makefile.PL && make;
  } || return 1
}

check() {
  cd "${srcdir}/${_pkg_root}"
  export PERL5LIB="" PERL_LOCAL_LIB_ROOT="" HARNESS_OPTIONS=j10
  make test || return 1;
}

package() {
  cd "${srcdir}/${_pkg_root}"
  export PERL5LIB="" PERL_LOCAL_LIB_ROOT=""
  make install || return 1
  # remove perllocal.pod
  find "$pkgdir" -name perllocal.pod -delete
}
